//@version=5
strategy('How to SL / TP', overlay=true, default_qty_type=strategy.percent_of_equity, default_qty_value=100, commission_type=strategy.commission.percent, commission_value=0.1)


// *** Indicators *** 
bb_length = 20
bb_mult = 2
bb_stddev = ta.stdev(close, bb_length)
bb_average = ta.sma(close, bb_length)
bb_upper = bb_average + bb_mult * bb_stddev
bb_lower = bb_average - bb_mult * bb_stddev


// *** Signals ***
entry_long = ta.crossover(close, bb_upper)
entry_short = ta.crossunder(close, bb_lower)


// *** Risk management *** 
entry_price = close
percent_diff = 5
rrr = 2
stop_loss_price = (1 - percent_diff / 100.) * entry_price 
take_profit_price = (1 + rrr * percent_diff / 100.) * entry_price 

stop_loss_in_ticks = (entry_price - stop_loss_price) / syminfo.mintick
take_profit_in_ticks = (take_profit_price - entry_price) / syminfo.mintick


// *** Positions *** 
if entry_long and strategy.position_size == 0
    strategy.entry("Long", strategy.long)
    strategy.exit("Long", loss = stop_loss_in_ticks, profit = take_profit_in_ticks, comment_loss = "SL Long", comment_profit = "TP Long")

if entry_short and strategy.position_size == 0
    strategy.entry("Short", strategy.short)
    strategy.exit("Short", loss = stop_loss_in_ticks, profit = take_profit_in_ticks, comment_loss = "SL Short", comment_profit = "TP Short")


// *** Plots *** 
plot(bb_upper, color=color.new(#ffb13b, 0), linewidth=2)
plot(bb_average, color=color.new(#b43bff, 0), linewidth=2)
plot(bb_lower, color=color.new(#ffb13b, 0), linewidth=2)
